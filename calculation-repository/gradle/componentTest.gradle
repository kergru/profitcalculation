/***********************************************************************************************************************
 * Component Test
 ***********************************************************************************************************************/

configurations {
    componentTestCompile.extendsFrom testCompile
    componentTestRuntime.extendsFrom testRuntime
    componentTestImplementation.extendsFrom testImplementation
    componentTestRuntimeOnly.extendsFrom testRuntimeOnly
    componentTestCompileOnly.extendsFrom testCompileOnly
}

sourceSets {
    componentTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output

        java.srcDirs = ['src/component-test/java']
        resources.srcDirs = ['src/component-test/resources']
    }
}

task componentTest(type: Test, description: 'Run component tests.') {
    useJUnitPlatform()

    group = "Verification"

    testClassesDirs = sourceSets.componentTest.output.classesDirs
    classpath = sourceSets.componentTest.runtimeClasspath

    systemProperty 'spring.profiles.active', 'dev'

    testLogging {
        events "passed", "skipped", "failed"
    }

    // always run this task
    outputs.upToDateWhen { false }
}
